# ~~~
# ####################################################################
# Description:  CMakeLists.txt
#
#               This file, 'CMakeLists.txt', implements build system
#               rules for Machinekit-HAL project
#
# Copyright (C) 2021    Jakub Fi≈°er  <jakub DOT fiser AT eryaf DOT com>
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
#
# #####################################################################
# ~~~

if(NOT DEFINED SYS_IO_AVAILABLE)
  check_include_files("sys/io.h" SYS_IO_AVAILABLE)
endif()

if(SYS_IO_AVAILABLE)
  option(BUILD_UPCI_LIBRARY "Built the UPCI library." TRUE)

  if(BUILD_UPCI_LIBRARY)
    add_library(upci STATIC)

    set(SOURCE_FILES ${CMAKE_CURRENT_SOURCE_DIR}/src/upci.c
                     ${CMAKE_CURRENT_SOURCE_DIR}/src/bitfile.c)
    set(PUBLIC_HEADER_FILES ${CMAKE_CURRENT_SOURCE_DIR}/include/upci/bitfile.h
                            ${CMAKE_CURRENT_SOURCE_DIR}/include/upci/upci.h)

    target_sources(
      upci
      PUBLIC ${PUBLIC_HEADER_FILES}
      PRIVATE ${SOURCE_FILES})

    target_include_directories(
      upci PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>)

    set_target_properties(
      upci
      PROPERTIES LIBRARY_OUTPUT_DIRECTORY "${CMAKE_INSTALL_LIBDIR}"
                 PUBLIC_HEADER "${PUBLIC_HEADER_FILES}"
                 POSITION_INDEPENDENT_CODE TRUE)

    install(
      TARGETS upci
      EXPORT machinekit_hal
      ARCHIVE DESTINATION "${MACHINEKIT_HAL_LIBRARY_DIRECTORY}"
              COMPONENT MachinekitHAL_Library_Development
      PUBLIC_HEADER DESTINATION "${MACHINEKIT_HAL_INTERFACE_DIRECTORY}"
                    COMPONENT MachinekitHAL_Library_Development)
  endif()
endif()
